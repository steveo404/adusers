Option Compare Database

Public Static Sub ADGroupImport()
    'Script used to import files from UserLookup.bat batch script - after the batch file is run
    'The batch script pulls the AD groups associated with a list of users. Each user generates a separate file.
    'This script is similar to the TxtUserListImport script and uses the same source files
    'The script allows you to choose the folder location (on the local machine) where the files are stored
    'This VBA script runs through each profile file and loads the AD groups assigned to the user into a text file
    'The text file is then loaded into a database table

    Dim strPath As String
    Dim strFile As String
    Dim strFilePath As String
    Dim header As String
    Dim entry As String
    Dim trigger As Integer
    Dim firstTime As Integer
    Dim entryGrab As String
    Dim userName As String
    Dim fullName As String
    Dim comment As String
    Dim empID As String
    Dim userGroup As String

    
    strPath = Get_Directory("Choose the folder with the files for reading") & "\"    'Call the Get_Directory function for the file path
                                                                                    'A \ is also appended to the file path so it can be used later

    'Remove the resultFile from a previous run of this script
    resultFile = "C:\Scripts\GroupResults.txt"
    Open resultFile For Append As #9
    Close #9
    Kill resultFile
    
    
    'Remove bogus Aud_NS file that is generated by the batch script
    bogusFile = strPath + "User_list_.txt"
    Open bogusFile For Append As #99
    Close #99
    Kill bogusFile
    
    strFile = Dir(strPath & "*.txt")    'Get the first file in the path
    
    While strFile <> ""                 'While there are still files to process the loop will continue
        trigger = 0
        firstTime = 0
        header = Mid(strFile, 11, (InStr(strFile, ".") - 11))     'Get the header from the file name to know which ID is being reviewed
        strFilePath = strPath + strFile
        Open strFilePath For Input As #1    'Open the text file
        Do Until EOF(1)
            Input #1, entry
            If entry = "The command completed successfully." Then Exit Do
            entryGrab = Mid(entry, 1, 10)
            If entryGrab = "User name " Then
                userName = Mid(entry, 30, Len(entry) - 29)
            End If
            If entryGrab = "Full Name " Then
                fullName = Mid(entry, 30, Len(entry) - 29)
            End If
            If entryGrab = "Comment   " Then
                comment = Mid(entry, 30, Len(entry) - 29)
                If InStr(entry, "-") > 0 Then
                    empID = Mid(entry, (InStr(entry, "-") + 2), 5)
                End If
            End If
            If entryGrab = "Global Gro" Then
                trigger = 1
                firstTime = 1
            End If
            If trigger > 0 Then
                strLength = (Len(entry))
                If strLength >= 52 Then
                    j = 30
                    For i = 1 To 2
                        userGroup = Trim(Mid(entry, j, 21))
                        j = j + 22
                        'Open the resultFile and poplate the header ane employee info
                        Open resultFile For Append As #9
                        Write #9, header, userName, fullName, comment, empID, userGroup
                        Close #9
                    Next
                    firstTime = 2
                End If
                'If the profile only has one entry - it requires a special check to pull in the AD group
                'The following code is only executed the first time through the profile looking for those entries
                'that only have one entry.  Then the <firstTime> variable is set to 2 so it is skipped and the second
                'If statement is avoided
                If firstTime = 1 Then
                    userGroup = Trim(Mid(entry, 30, 21))
                    'Open the resultFile and poplate the header ane employee info
                    Open resultFile For Append As #9
                    Write #9, header, userName, fullName, comment, empID, userGroup
                    Close #9
                Else    'Second If statement avoided during first time if the length is less than 52 characters
                    If (strLength > 21) And (strLength < 52) Then
                        j = 1
                        For i = 1 To 2
                            userGroup = Trim(Mid(entry, j, 22))
                            j = j + 22
                            'Open the resultFile and poplate the header ane employee info
                            Open resultFile For Append As #9
                            Write #9, header, userName, fullName, comment, empID, userGroup
                            Close #9
                        Next
                    End If
                End If
                If (strLength < 22) Then
                    userGroup = Trim(Mid(entry, 1, 21))
                    'Open the resultFile and poplate the header ane employee info
                    Open resultFile For Append As #9
                    Write #9, header, userName, fullName, comment, empID, userGroup
                    Close #9
                End If
                firstTime = 2
            End If
        Loop
         
        userName = ""
        fullName = ""
        comment = ""
        empID = ""
        userGroup = ""
        
        Close #1        'Close the text file
        strFile = Dir() 'Pull the next file in the path
    Wend
    
    Dim strTableName As String
    Dim fileDate As String
    
    fileDate = TodayDate()
    
    'Remove previous version of this file if it was run earlier in the dat
    'It now has a date stamp included with the file name
    
    strTableName = "ADMemberships" + fileDate
    If TableExists(strTableName) Then
        DoCmd.DeleteObject acTable, strTableName
    End If
    
    'Import the text file into a database table
    DoCmd.TransferText acImportDelim, , strTableName, resultFile, False
    
    Application.RefreshDatabaseWindow
    
    MsgBox "Script Complete!!!"

End Sub